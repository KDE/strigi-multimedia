AC_DEFUN([KDE_CHECK_THEORA],
[
have_theora=yes

KDE_CHECK_HEADER(theora/theora.h,
  [], [have_theora=no])

KDE_CHECK_LIB(theora, theora_info_init,
  [], [have_theora=no], [-lvorbis -logg])
])

AC_ARG_WITH(theora,
  [AC_HELP_STRING(--with-theora,
    [enable support for Ogg Theora @<:@default=check@:>@])],
  [], with_theora=check)

have_theora=no
if test "x$with_theora" != xno; then
  KDE_CHECK_THEORA

  if test "x$with_theora" != xcheck && test "x$have_theora" != xyes; then
    AC_MSG_ERROR([--with-theora was given, but test for Theora failed])
  fi
fi

AM_CONDITIONAL(include_theora_SUBDIR, test "x$have_theora" = xyes)
